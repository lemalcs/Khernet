@echo off

echo Starting to create release...

set current_script_path=%~dp0
cd %current_script_path%

rem Create directory for .Net 4.5 used for nuget package
set lib_directory=%current_script_path%src\lib\net45
IF not exist %lib_directory% mkdir %lib_directory%

rem Copy to lib_directory any other files needed to update the application
rem Add copy command here

rem Create nuget package
%~dp0..\..\..\tools\nuget.exe pack src\Khernet.nuspec

rem Create release directory
set release_directory=..\\..\\..\\bin\\Releases
IF not exist %release_directory% mkdir %release_directory%

rem Create squirrel installer (nuget package) 
set package_version=0.15.10
set package_name=Khernet
%~dp0..\..\..\tools\squirrel.exe --releasify %package_name%.%package_version%.nupkg --releaseDir=%release_directory% --no-msi

set temp_directory=temp
set path_7zip=%~dp0..\..\..\tools\7z.exe
rem Extract files from nuget package generated by squirrel
%path_7zip% e %release_directory%\%package_name%-%package_version%-full.nupkg -o%temp_directory% khernetlauncher_ExecutionStub.exe -r -y

rem Copy stub executable (native launcher) to bin directory
copy %temp_directory%\khernetlauncher_ExecutionStub.exe %~dp0..\..\..\bin\Khernet.Installer\khernetlauncher.exe

rem Extract updater file used by squirrel framework
%path_7zip% e %release_directory%\Setup.exe -o%temp_directory% Update.exe -r -y

rem Copy updater to bin directory
copy %temp_directory%\Update.exe %~dp0..\..\..\bin\Khernet.Installer\